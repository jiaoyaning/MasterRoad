import com.android.build.gradle.internal.dsl.*

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.protobuf'
apply plugin: 'com.alibaba.arouter'

//apply plugin: 'me.ele.lancet' //饿了么字节码hook库
//apply plugin: 'com.jyn.helloworld'
apply from: 'SimplePlugin.gradle' //自定义极简插件，路径方式引入

/*
 * 配置构建 官方文档
 * https://developer.android.com/studio/build?hl=zh-cn
 *
 * 补齐Android技能树 - 玩转Gradle(一)
 * https://juejin.cn/post/6950643579643494431
 * 补齐Android技能树——从AGP构建过程到APK打包过程
 * https://juejin.cn/post/6963527524609425415
 * 补齐Android技能树 - 从害怕到玩转Android代码混淆
 * https://juejin.cn/post/6966526844552085512
 *
 * 连载 | 深入理解Gradle框架之一：Plugin, Extension, buildSrc
 * https://mp.weixin.qq.com/s/mDCTtQZb6mhWOFAvLYKBSg
 * 连载 | 深入理解gradle框架之二：依赖实现分析
 * https://mp.weixin.qq.com/s/WQCqUaYDPHDIjUHlxjRIkw
 * 连载 | 深入理解gradle框架之三：artifacts的发布
 * https://mp.weixin.qq.com/s/-G0OHJrNHbjXTC7AHtVAKw
 */
def app = rootProject.ext

android {
    compileSdkVersion app.compileSdkVersion
    buildToolsVersion app.buildToolsVersion

    defaultConfig {
        applicationId app.applicationId
        minSdkVersion app.minSdkVersion
        targetSdkVersion app.targetSdkVersion
        versionCode app.versionCode
        versionName app.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true

        /**
         * {@link JavaCompileOptions}
         * @see JavaCompileOptions#annotationProcessorOptions(Action)
         * {@link AnnotationProcessorOptions}
         * @see AnnotationProcessorOptions#argument
         */
        javaCompileOptions {
            annotationProcessorOptions {
                argument("AROUTER_MODULE_NAME", project.getName())
                argument("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
    }

    /*
     * R8编译器官方文档
     * https://developer.android.google.cn/studio/build/shrink-code
     *
     * Android 混淆，新引入的D8、R8改变了什么？
     * https://mp.weixin.qq.com/s/PxrW7LSDuAAeqlXFxnIInA
     */
    buildTypes {
        release {
            minifyEnabled true //R8 代码缩减功能
            shrinkResources true //缩减资源 : 资源缩减只有在与代码缩减配合使用时才能发挥作用
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    /*
     * kotlin-android-extensions插件被废弃，推荐使用viewBinding
     * https://mp.weixin.qq.com/s/keR7bO-Nu9bBr5Nhevbe1Q
     */
    buildFeatures {
        dataBinding true
        viewBinding true
//        compose true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    kotlinOptions.useIR = true
}
/*
 * https://www.jianshu.com/p/cc98a6b4f52e
 * gradle 高级用法
 *
 * compile : 会传递依赖
 * implementation : 不会传递依赖
 *      子依赖的子依赖修改代码时，可以不用进行全部编译
 * api : 会传递依赖，代替compile
 */
dependencies {
    implementation 'androidx.asynclayoutinflater:asynclayoutinflater:1.0.0'
    def test = app.test
    def androidx = app.androidx
    def kotlin = app.kotlin
    def http = app.http
    def utils = app.utils

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':java_kotlin')
    implementation project(path: ':Leetcode')
    implementation project(path: ':common')

    implementation project(path: ':Plugin:APT')
    kapt project(path: ':Plugin:APT-processor')

    testImplementation test["junit"]
    androidTestImplementation test["ext-junit"]
    androidTestImplementation test["espresso-core"]
    androidTestImplementation test["espresso-contrib"]
    androidTestImplementation test["espresso-intents"]
    androidTestImplementation test["test-rules"]
    androidTestImplementation test["test-runner"]
    androidTestImplementation test["test:core"]
    androidTestImplementation test["test:core-ktx"]
    androidTestImplementation test["test.ext:truth"]

    implementation androidx["material"]
    implementation androidx["appcompat"]
    implementation androidx["annotation"]
    implementation androidx["startup"]
    implementation androidx["recyclerview"]
    implementation androidx["constraintlayout"]
    implementation androidx["lifecycle"]
    implementation androidx["lifecycle-process"]
    implementation androidx["viewmodel"]
    implementation androidx["livedata"]
    implementation androidx["core-ktx"]
    implementation androidx["viewmodel-ktx"]
    implementation androidx["livedata-ktx"]
    implementation androidx["runtime-ktx"]
    implementation androidx["activity-ktx"] //协程用得到
    implementation androidx["fragment-ktx"]
    implementation androidx["hilt"]
    kapt androidx["hilt-compiler"]
    implementation androidx["hilt-viewmodel"]
    kapt androidx["hilt-compiler2"]

    implementation kotlin["kotlin"]
    implementation kotlin["coroutines-core"]
    implementation kotlin["coroutines-android"]
    implementation kotlin["coroutines-jdk8"]
    implementation kotlin["koin"]
    implementation kotlin["koin-android"]
    implementation kotlin["koin-android-ext"]
    implementation kotlin["reflect"]
    implementation kotlin["coil"]

    implementation http["okio"]
    implementation http["okhttp3"]
    implementation http["okhttp3-logging"]
    implementation http["retrofit2"]
    implementation http["retrofit2-scalars"]
    implementation http["retrofit2-gson"]
    implementation http["retrofit2-rxjava3"]
    implementation http["rxjava3"]
    implementation http["rxandroid3"]
    implementation 'com.trello.rxlifecycle4:rxlifecycle:4.0.2'
    implementation 'com.trello.rxlifecycle4:rxlifecycle-android:4.0.2'
    implementation 'com.trello.rxlifecycle4:rxlifecycle-kotlin:4.0.2'
    implementation 'com.trello.rxlifecycle4:rxlifecycle-android-lifecycle-kotlin:4.0.2'
    implementation http["rxhttp"]
    kapt http["rxhttp-compiler"]

    implementation utils["glide"]
    implementation utils["glide-integration"]
    annotationProcessor utils["glide-compiler"]
    implementation utils["fresco"]
    implementation utils["ultimatebarx"]
    implementation utils["eventbus"]
    implementation utils["mmkv"]
    debugImplementation utils["leakcanary"]
    debugImplementation utils["blockcanary"]
    implementation utils["arouter"]
    annotationProcessor utils["arouter-compiler"]
    kapt utils["arouter-compiler"]
    implementation utils["codelocator"]

    //https://github.com/evant/binding-collection-adapter
    implementation utils["bindingcollectionadapter"]
    implementation utils["bindingcollectionadapter-recyclerview"]
    implementation utils["bindingcollectionadapter-viewpager2"]




//    implementation "com.kuaishou.koom:koom-java-leak:2.0.0"

    //只引入，不参与打包，这样即可在External Libraries 中看到其源码了
//    compileOnly 'com.android.tools.build:gradle:4.1.3'

    /**
     * Hook框架
     * https://github.com/eleme/lancet
     */
//    compileOnly 'me.ele:lancet-base:1.0.6'

    /**
     * https://github.com/didi/DoraemonKit
     */
//    debugImplementation 'io.github.didi.dokit:dokitx:3.5.0.1'
//    releaseImplementation 'io.github.didi.dokit:dokitx-no-op:3.5.0.1'
}

/**
 * 打印所有的Task
 */
gradle.taskGraph.whenReady {
    println("taskGraph 开始 =============")
    it.getAllTasks().each { task ->
        println "${task.name} : ${task.class.name - '_Decorated'}"
    }
    println("taskGraph 结束 =============")
}

afterEvaluate {
    println("3. 插入第三顺位想插入的代码 -> app/build.gradle")
}